apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion build_versions.compile_sdk

    signingConfigs {
        config {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
        }
    }

    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "com.zerofate.template.justfortest"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.traget_sdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    def env
    buildTypes {
        debug {
            env = "0"
            signingConfig signingConfigs.config
        }
        release {
            env = "1"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        buildTypes.each {
            it.buildConfigField("int", "ENV", env)
        }
    }
    flavorDimensions("default")
    productFlavors {
        tencent {
            dimension "default"
        }
        baidu {
            dimension "default"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
android.applicationVariants.all { variant ->
    variant.outputs.all {
        def date = new Date().format("MMddHH")
        def productFlavorName = variant.productFlavors[0].name
        outputFileName = "应用名称" + "-${variant.versionName}-" + productFlavorName + "-" + date + "-${variant.buildType.name}" + ".apk"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.support.appcompat
    implementation deps.constraint_layout
    testImplementation deps.junit
    implementation deps.support.recyclerview
    implementation deps.butterknife.runtime
    annotationProcessor deps.butterknife.processor
    implementation project(':zerosdk')
    implementation deps.permissionsdispatcher.runtime
    annotationProcessor deps.permissionsdispatcher.processor
    implementation deps.dagger.runtime
    annotationProcessor deps.dagger.processor
    implementation deps.gson
    implementation deps.support.v4
    implementation deps.lifecycle.extensions
    implementation deps.retrofit.runtime
    implementation deps.support.design
    implementation project(':core')
    implementation deps.rxpermissions
}
repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }//rxpermissions
}
